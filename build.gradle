// Project-wide build file with configuration options common to all submodules.
buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'com.android.application' version '8.1.0' apply false
    id 'com.google.gms.google-services' version '4.3.15' apply false
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin' version '2.0.1' apply false
}

subprojects {
    afterEvaluate {
        if (plugins.hasPlugin('com.android.library') ||
                plugins.hasPlugin('com.android.application')) {
            android {
                compileSdkVersion 33
                buildToolsVersion '33.0.2'
                ndkVersion '23.2.8568313'

                lint {
                    checkReleaseBuilds false
                    disable 'MissingTranslation', 'ExtraTranslation', 'BlockedPrivateApi'
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_11
                    targetCompatibility JavaVersion.VERSION_11

                    coreLibraryDesugaringEnabled true
                }

                defaultConfig {
                    minSdkVersion 21
                    targetSdkVersion 33
                }

                buildTypes {
                    play {
                        initWith release
                    }
                }
            }

            dependencies {
                coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.3'
            }
        }
    }
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}